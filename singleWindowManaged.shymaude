in singleWindow.shymaude

fmod PARAMETERS is
	pr	NAT .
	pr	FLOAT .

	ops T dt : -> Float .
	op N : -> Nat .
	ops L0 L1 : -> Nat .
	ops minDeliveryTime maxDeliveryTime
		minSubmitTime maxSubmitTime
		minProcessTime maxProcessTime
		minProcessingTime maxProcessingTime
		minManagingTime maxManagingTime
		: -> Float .

	eq	T = 40.0 .
	eq	dt = 0.1 .
	eq minDeliveryTime = 0.1 .
	eq maxDeliveryTime = 0.2 .
	eq minSubmitTime = 0.1 .
	eq maxSubmitTime = 0.2 .
	eq minProcessTime = 0.05 .
	eq maxProcessTime = 0.1 .
	eq minProcessingTime = 0.5 .
	eq maxProcessingTime = 1.5 .
	eq minManagingTime = 4.0 .
	eq maxManagingTime = 4.05 .
	eq N = 7 .
	eq L0 = 50 .
	eq L1 = 80 .
endfm

shymod OFFICER-STATE is
	sort OfficerState .
	ops idle busy : -> OfficerState [ctor] .
endshy

shymod OFFICER is
	pr COMMANDS .
	pr OFFICER-STATE .
	class Officer is disc
		state : OfficerState,
		releasing : Bool
	endclass
endshy

shymod MANAGER-COMMANDS is
	pr COMMANDS .
	op manageOfficers : -> Attribute [ctor] .
	op releaseOfficer : -> Attribute [ctor] .
endshy

shymod MANAGER is
	pr MANAGER-COMMANDS .
	class Manager is disc
		numOfficers : Nat
	endclass
endshy

shymod MANAGER-DYNAMICS is
	pr	SYSTEM .
	pr	OFFICER .
	pr	MANAGER .
	
	var t timeStep : Float .
	var conf : Conf .
	var attrSet : AttributeSet .
	var id numOfficers currentAppId : Nat .
	var appQueue : AppQueue .

	crl < id : Manager | numOfficers : s numOfficers >
		< id <- manageOfficers >
		=>
		< id : Manager | numOfficers : s numOfficers >
		if numOfficers == 0 .
	
	crl	{
			< id : Manager | numOfficers : s numOfficers >
			< id <- manageOfficers >
			queue(appQueue)
			conf, time(t), app(currentAppId)
		}
		=>
		if queueLength(queue(appQueue)) > L1
			then
				{
					< s s numOfficers : Officer | state : idle, releasing : false >
					< id : Manager | numOfficers : s s numOfficers >
					[ sampleUniform(minManagingTime, maxManagingTime), < 1 <- manageOfficers > ]
					queue(appQueue)
					conf, time(t), app(currentAppId)
				}
			else
				if queueLength(queue(appQueue)) < L0
					then
						{
							< id : Manager | numOfficers : numOfficers >
							[ sampleUniform(minDeliveryTime, maxDeliveryTime), < s numOfficers <- releaseOfficer > ]
							[ sampleUniform(minManagingTime, maxManagingTime), < 1 <- manageOfficers > ]
							queue(appQueue)
							conf, time(t), app(currentAppId)
						}
					else
						{
							< id : Manager | numOfficers : s numOfficers >
							[ sampleUniform(minManagingTime, maxManagingTime), < 1 <- manageOfficers > ]
							queue(appQueue)
							conf, time(t), app(currentAppId)
						}
				fi
		fi
	if numOfficers > 0 .
				
	eq	processTick(< id : Manager | attrSet > conf, timeStep) = < id : Manager | attrSet > processTick(conf, timeStep) .
endshy

shymod OFFICER-DYNAMICS is
	pr	SYSTEM .
	pr	OFFICER .
	pr	MANAGER-COMMANDS .
	pr	BASE-OFFICER-DYNAMICS .
	
	var t timeStep : Float .
	var conf : Conf .
	var appQueue : AppQueue .
	var attrSet : AttributeSet .
	vars id appId clientId currentAppId : Nat .
	var submitTime : Float .
	var missingDocs : Set{Class} .
	var releasing : Bool .
	
	*** Indicate realeasing
	rl	< id : Officer | releasing : releasing, attrSet >
		< id <- releaseOfficer >
		=>
		< id : Officer | releasing : true, attrSet > .

	*** Add process and accept scheduled messages
	rl	{
			< id : Officer | state : busy, releasing : releasing, attrSet >
			< id <- addProcessAndAccept, clientId : clientId, appId : appId, submitTime : submitTime >
			conf
			queue(appQueue), time(t), app(currentAppId)
		}
		=>
		{
			if releasing
				then none
				else
					< id : Officer | state : idle, releasing : false, attrSet >
					[ sampleUniform(minProcessTime, maxProcessTime), < id <- process > ]
			fi
			[ sampleUniform(minDeliveryTime, maxDeliveryTime), < clientId <- accepted : appId, submitTime : submitTime > ]
			conf
			queue(appQueue), time(t), app(currentAppId)
		} .

	*** Add process and incomplete scheduled messages
	rl
		{
			< id : Officer | state : busy, releasing : releasing, attrSet >
			< id <- addProcessAndIncomplete, clientId : clientId, appId : appId, missingDocs : missingDocs >
			conf
			queue(appQueue), time(t), app(currentAppId)
		}
		=>
		{
			if releasing
				then none
				else
					< id : Officer | state : idle, releasing : false, attrSet >
					[ sampleUniform(minProcessTime, maxProcessTime), < id <- process > ]
			fi
			[ sampleUniform(minDeliveryTime, maxDeliveryTime), < clientId <- incomplete : appId, missing : missingDocs > ]
			conf
			queue(appQueue), time(t), app(currentAppId)
		} .
endshy

shymod SYSTEM-DYNAMICS is
	inc CLIENT-DYNAMICS .
	inc OFFICER-DYNAMICS .
	inc MANAGER-DYNAMICS .
	inc DOC-DYNAMICS .

	op	initState : -> Config [ctor] .
	
	op	initOfficers : Nat -> ActiveConf [ctor] .
	
	var id : Nat .
	
	eq	initState =  {
			< 1 : Client | accepted : 0, incomplete : 0, docProcessingTimeMean : 0.0, docProcessingTimeSquaredMean : 0.0, docProcessed : 0 >
			< 1 : Manager | numOfficers : N >
			[ sampleUniform(minSubmitTime, maxSubmitTime), < 1 <- submit > ]
			[ sampleUniform(minManagingTime, maxManagingTime), < 1 <- manageOfficers > ]
			initOfficers(N)
			queue(empty)
			nextId(1)
			, time(0.0), app(0)
		} .
	
	rl	initOfficers(s id)
		=>
		< s id : Officer | state : idle, releasing : false >
		initOfficers(id)
		[ sampleUniform(minProcessTime, maxProcessTime), < s id <- process > ] .
		
	eq	initOfficers(0) = none .

endshy